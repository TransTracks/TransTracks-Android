apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'io.fabric'
apply plugin: 'realm-android'

android {
    compileSdkVersion 28

    defaultConfig {
        applicationId "com.drspaceboo.transtracks"
        minSdkVersion 21
        targetSdkVersion 28
        versionCode 10
        versionName "1.0.9"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        buildTypes.each {
            it.buildConfigField 'String', 'ADS_APP_ID', '"' + getSecretString('ADS_APP_ID') + '"'
            it.resValue 'string', 'ADS_APP_ID', getSecretString('ADS_APP_ID')
            it.resValue 'string', 'ADS_GALLERY_AD_ID', getSecretString('ADS_GALLERY_AD_ID')
            it.resValue 'string', 'ADS_HOME_AD_ID', getSecretString('ADS_HOME_AD_ID')
            it.resValue 'string', 'ADS_MILESTONES_AD_ID', getSecretString('ADS_MILESTONES_AD_ID')
            it.resValue 'string', 'ADS_SETTINGS_AD_ID', getSecretString('ADS_SETTINGS_AD_ID')
            it.buildConfigField 'String', 'CODE_SALT', '"' + getSecretString('CODE_SALT') + '"'
        }

        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    implementation 'androidx.appcompat:appcompat:1.0.2'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.0-alpha3'
    implementation 'androidx.recyclerview:recyclerview:1.0.0'

    implementation 'com.bluelinelabs:conductor:2.1.5'

    implementation 'com.crashlytics.sdk.android:crashlytics:2.9.8'

    implementation 'com.f2prateek.rx.preferences2:rx-preferences:2.0.0'

    implementation 'com.google.android.gms:play-services-ads:17.1.3'
    //Custom tabs & Support V4 are only required here to replace the old version in the play-services-ads module
    implementation 'androidx.browser:browser:1.0.0'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'

    implementation 'com.google.firebase:firebase-core:16.0.7'

    implementation 'com.google.android.material:material:1.1.0-alpha03'

    implementation "com.jakewharton.rxbinding3:rxbinding:$rx_binding_version"
    implementation "com.jakewharton.rxbinding3:rxbinding-recyclerview:$rx_binding_version"
    implementation "com.jakewharton.rxbinding3:rxbinding-material:$rx_binding_version"
    implementation "com.jakewharton.rxbinding3:rxbinding-appcompat:$rx_binding_version"

    implementation 'com.jakewharton.rxrelay2:rxrelay:2.0.0'

    implementation 'com.jakewharton.threetenabp:threetenabp:1.0.5'

    implementation 'com.squareup.picasso:picasso:2.71828'
    //Exif interface is only required here to replace the old version in the picasso module
    implementation 'androidx.exifinterface:exifinterface:1.0.0'

    debugImplementation 'com.squareup.leakcanary:leakcanary-android:1.6.1'
    releaseImplementation 'com.squareup.leakcanary:leakcanary-android-no-op:1.6.1'

    implementation 'io.reactivex.rxjava2:rxjava:2.2.6'
    implementation 'io.reactivex.rxjava2:rxandroid:2.1.0'

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

    testImplementation 'junit:junit:4.12'
    testImplementation 'org.threeten:threetenbp:1.3.4'

    androidTestImplementation 'androidx.test:runner:1.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.1.1'
}

String getSecretString(String key) {
    Properties props = new Properties()
    props.load(new FileInputStream(file("$rootDir/secrets.properties")))
    return props[key]
}

apply plugin: 'com.google.gms.google-services'
