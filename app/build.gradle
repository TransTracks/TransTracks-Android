plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'io.realm.kotlin'
}

android {
    compileSdkVersion 33

    defaultConfig {
        applicationId "com.drspaceboo.transtracks"
        minSdkVersion 21
        targetSdkVersion 33
        versionCode 25
        versionName "1.3.2"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    compileOptions {
        coreLibraryDesugaringEnabled true
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = "1.8"
    }

    buildTypes {
        buildTypes.each {
            it.resValue 'string', 'ADS_APP_ID', getSecretString('ADS_APP_ID')
            it.resValue 'string', 'ADS_GALLERY_AD_ID', getSecretString('ADS_GALLERY_AD_ID')
            it.resValue 'string', 'ADS_HOME_AD_ID', getSecretString('ADS_HOME_AD_ID')
            it.resValue 'string', 'ADS_MILESTONES_AD_ID', getSecretString('ADS_MILESTONES_AD_ID')
            it.resValue 'string', 'ADS_SETTINGS_AD_ID', getSecretString('ADS_SETTINGS_AD_ID')
            it.buildConfigField 'String', 'CODE_SALT', '"' + getSecretString('CODE_SALT') + '"'
        }

        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    buildFeatures {
        viewBinding true
    }
    namespace 'com.drspaceboo.transtracks'
}

dependencies {
    coreLibraryDesugaring 'com.android.tools:desugar_jdk_libs:2.0.3'

    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'androidx.core:core-ktx:1.10.0'
    implementation 'androidx.recyclerview:recyclerview:1.3.0'
    implementation 'androidx.exifinterface:exifinterface:1.3.6'

    implementation 'com.bluelinelabs:conductor:2.1.5'

    implementation 'com.google.code.gson:gson:2.10.1'

    implementation 'com.firebaseui:firebase-ui-auth:8.0.2'

    implementation platform('com.google.firebase:firebase-bom:31.5.0')
    implementation 'com.google.firebase:firebase-ads:22.0.0'
    implementation 'com.google.firebase:firebase-analytics-ktx'
    implementation 'com.google.firebase:firebase-auth-ktx'
    implementation 'com.google.firebase:firebase-crashlytics-ktx'
    implementation 'com.google.firebase:firebase-firestore-ktx'

    implementation 'com.google.android.material:material:1.9.0'

    ext.rx_binding_version = '3.1.0'
    implementation "com.jakewharton.rxbinding3:rxbinding:$rx_binding_version"
    implementation "com.jakewharton.rxbinding3:rxbinding-recyclerview:$rx_binding_version"
    implementation "com.jakewharton.rxbinding3:rxbinding-material:$rx_binding_version"
    implementation "com.jakewharton.rxbinding3:rxbinding-appcompat:$rx_binding_version"

    implementation 'com.jakewharton.rxrelay2:rxrelay:2.1.1'

    implementation 'com.squareup.picasso:picasso:2.71828'

    debugImplementation 'com.squareup.leakcanary:leakcanary-android:2.10'

    implementation 'io.reactivex.rxjava2:rxjava:2.2.21'
    implementation 'io.reactivex.rxjava2:rxandroid:2.1.1'

    implementation 'io.realm.kotlin:library-base:1.8.0'

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-rx3:1.6.4'

    testImplementation 'junit:junit:4.13.2'

    androidTestImplementation 'androidx.test:runner:1.5.2'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
}

String getSecretString(String key) {
    Properties props = new Properties()
    props.load(new FileInputStream(file("$rootDir/secrets.properties")))
    return props[key]
}

apply plugin: 'com.google.gms.google-services'
apply plugin: 'com.google.firebase.crashlytics'
