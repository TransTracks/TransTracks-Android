apply plugin: 'com.android.application'
apply plugin: 'realm-android'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'io.fabric'

android {
    compileSdkVersion 27

    defaultConfig {
        applicationId "com.drspaceboo.transtracker"
        minSdkVersion 21
        targetSdkVersion 27
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        manifestPlaceholders = [FABRIC_API_KEY: getSecretString('FABRIC_API_KEY')]
    }

    buildTypes {
        buildTypes.each {
            it.buildConfigField 'String', 'ADS_APP_ID', '"' + getSecretString('ADS_APP_ID') + '"'
            it.resValue 'string', 'ADS_HOME_AD_ID', getSecretString('ADS_HOME_AD_ID')
            it.resValue 'string', 'ADS_SETTINGS_AD_ID', getSecretString('ADS_SETTINGS_AD_ID')
        }

        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    implementation "com.android.support:appcompat-v7:$support_version"
    implementation "com.android.support:design:$support_version"
    implementation 'com.android.support.constraint:constraint-layout:1.1.0'

    implementation 'com.google.android.gms:play-services-ads:15.0.1'
    //Custom tabs & Support V4 are only required here to replace the old version in the play-services-ads module
    implementation "com.android.support:customtabs:$support_version"
    implementation "com.android.support:support-v4:$support_version"

    implementation 'com.bluelinelabs:conductor:2.1.4'

    implementation('com.crashlytics.sdk.android:crashlytics:2.9.1@aar') {
        transitive = true;
    }

    compile 'com.f2prateek.rx.preferences2:rx-preferences:2.0.0'

    implementation 'com.jakewharton:kotterknife:0.1.0-SNAPSHOT'
    //Recycler view is only required here to replace the old version in the kotterknife module
    implementation "com.android.support:recyclerview-v7:$support_version"

    implementation "com.jakewharton.rxbinding2:rxbinding-kotlin:$rx_binding_version"
    implementation "com.jakewharton.rxbinding2:rxbinding-recyclerview-v7-kotlin:$rx_binding_version"
    implementation "com.jakewharton.rxbinding2:rxbinding-design-kotlin:$rx_binding_version"
    implementation "com.jakewharton.rxbinding2:rxbinding-appcompat-v7-kotlin:$rx_binding_version"

    implementation 'com.jakewharton.rxrelay2:rxrelay:2.0.0'

    implementation 'com.jakewharton.threetenabp:threetenabp:1.0.5'

    implementation 'com.squareup.picasso:picasso:2.71828'
    //Exif interface is only required here to replace the old version in the picasso module
    implementation "com.android.support:exifinterface:$support_version"

    debugImplementation 'com.squareup.leakcanary:leakcanary-android:1.5.4'
    releaseImplementation 'com.squareup.leakcanary:leakcanary-android-no-op:1.5.4'

    implementation 'io.reactivex.rxjava2:rxjava:2.1.5'
    implementation 'io.reactivex.rxjava2:rxandroid:2.0.1'

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

    testImplementation 'junit:junit:4.12'
    testImplementation 'org.threeten:threetenbp:1.3.4'

    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
}

String getSecretString(String key) {
    Properties props = new Properties()
    props.load(new FileInputStream(file("$rootDir/secrets.properties")))
    return props[key]
}
